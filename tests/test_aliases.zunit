#!/usr/bin/env zunit

@setup {
    load "../you-should-use.plugin.zsh"
}

@teardown {
}

@test 'aliases no match' {
    alias ls="less -R"
    run _check_aliases "less"

    assert "$output" is_empty
    assert $state equals 0
}

@test 'aliases longer or the same length as their commands are ignored' {
    alias longalias='ls -lA'
    alias ls_-lA='ls -lA'
    run _check_aliases "ls -lA"

    assert "$output" is_empty
    assert $state equals 0
}

@test 'aliases match - all' {
    export YSU_MODE="ALL"
    alias gs="git status"
    alias g="git"
    alias gsv="git status -v"
    run _check_aliases "git status -v"

    assert "$lines[1]" same_as '\033[1mFound existing alias for "git". You should use: "g"\033[00m'
    assert "$lines[2]" same_as '\033[1mFound existing alias for "git status". You should use: "gs"\033[00m'
    assert "$lines[3]" same_as '\033[1mFound existing alias for "git status -v". You should use: "gsv"\033[00m'
    assert $state equals 0
}

@test 'aliases match - bestmatch' {
    export YSU_MODE="BESTMATCH"
    alias gs="git status"
    alias g="git"
    alias gsv="git status -v"
    run _check_aliases "git status -v"

    assert "$output" same_as '\033[1mFound existing alias for "git status -v". You should use: "gsv"\033[00m'
    assert $state equals 0
}
